plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'io.vitaliivorobii.redis.netty.bridge'
version = '1.0-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'redis-netty-bridge-core'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'redis-netty-bridge-core'
                description = 'Redis netty bridge core'
                url = 'https://github.com/vorobii-vitalii/redis-netty-bridge'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'vorobii-vitalii'
                        name = 'Vitalii Vorobii'
                        email = 'vitalij.vorobij@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/vorobii-vitalii/redis-netty-bridge.git'
                    developerConnection = 'scm:git:ssh://github.com/vorobii-vitalii/redis-netty-bridge.git'
                    url = 'https://github.com/vorobii-vitalii/redis-netty-bridge'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/vorobii-vitalii/redis-netty-bridge-core"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/vorobii-vitalii/resp-netty")
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:4.0.0-M1'
    testImplementation 'org.mockito:mockito-core:5.18.0'

    // netty
    implementation "io.netty:netty-all:5.0.0.Alpha2"

    // logging facade
    implementation "org.slf4j:slf4j-api:2.1.0-alpha1"

    implementation 'io.vavr:vavr:1.0.0-alpha-4'

    implementation 'io.github.vorobii-vitalii:resp-netty:1.1.0'

}

test {
    useJUnitPlatform()
}